plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.112'
}

neoForge.version = "21.1.209"
version = "2.2.7"
group = "dev.gigaherz.toolbelt"
base.archivesName = "ToolBelt-${neoForge.minecraftVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

java.withSourcesJar()

sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

repositories {
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "Tterrag maven"
        url "https://maven.tterrag.com/"
    }
    maven { // CURIOS
        name = "C4 maven"
        url "https://maven.theillusivec4.top/"
    }
    maven {
        name = "Dogforce Games maven"
        url 'https://www.dogforce-games.com/maven/'
    }
    maven {
        name = "Cursemaven"
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        // Accessories
        name = "Wispforest maven"
        url 'https://maven.wispforest.io/releases'
    }
    //maven { url 'https://maven.su5ed.dev/releases' }
    //maven { url 'https://maven.fabricmc.net' }
    maven { url 'https://maven.shedaniel.me/' }
    maven {
        url "$projectDir/../CommonMaven"
    }
    mavenLocal()
}

neoForge {

    runs {
        // applies to all the run configs below
        configureEach {
            gameDirectory = project.file('run')
            logLevel = org.slf4j.event.Level.DEBUG
            //jvmArgument "-Dmixin.debug.export=true"
            //jvmArgument "-Dmixin.debug.verbose=true"
        }

        client {
            client()
        }

        client2 {
            client()

            programArguments.addAll '--username', 'Dev2'
        }

        server {
            server()
        }

        data {
            data()
            programArguments.addAll '--mod', 'toolbelt', '--all', '--output', file('src/main/generated_resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }

    mods {
        thismod {
            sourceSet(sourceSets.main)
        }
    }

    parchment {
        minecraftVersion = "1.21"
        mappingsVersion = "2024.11.10"
    }
}

dependencies {
    compileOnly "dev.gigaherz.sewingkit:SewingKit-1.21:2.0.8"
    runtimeOnly "dev.gigaherz.sewingkit:SewingKit-1.21:2.0.8"

    compileOnly "mezz.jei:jei-1.21.1-common-api:19.21.0.247"
    runtimeOnly "mezz.jei:jei-1.21.1-common:19.21.0.247"
    compileOnly "mezz.jei:jei-1.21.1-neoforge-api:19.21.0.247"
    runtimeOnly "mezz.jei:jei-1.21.1-neoforge:19.21.0.247"

    compileOnly "top.theillusivec4.curios:curios-neoforge:9.2.2+1.21.1"
    runtimeOnly "top.theillusivec4.curios:curios-neoforge:9.2.2+1.21.1"

    //compileOnly ("curse.maven:controllable-317269:6850105")
    //runtimeOnly ("curse.maven:controllable-317269:6850105")

    //compileOnly ("curse.maven:framework-549225:6531439")
    //runtimeOnly ("curse.maven:framework-549225:6531439")

    runtimeOnly ("curse.maven:sophisticated-backpacks-422301:7057664")
    runtimeOnly ("curse.maven:sophisticated-core-618298:7076819")

    //implementation ("curse.maven:lambdynamiclights-unofficial-neoforge-1130792:5980174")
}

jar {
    from(files('LICENSE.txt'))

    manifest {
        attributes([
                "Specification-Title": "toolbelt",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": "toolbelt",
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.named('sourcesJar') {
    from(files('LICENSE.txt'))
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
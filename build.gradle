buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'maven-publish'

repositories {
    maven { // JEI
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://maven.tterrag.com/"
    }
    maven { // CURIOS
        url "https://maven.theillusivec4.top/"
    }
    maven {
        url 'https://www.dogforce-games.com/maven/'
    }
    maven {
        url "https://www.cursemaven.com"
    }
    repositories {
        maven {
            url "$projectDir/../CommonMaven"
        }
    }
    mavenLocal()
}

version = "1.18.8"
group = "dev.gigaherz.toolbelt"
archivesBaseName = "ToolBelt-UNKNOWN"

project.afterEvaluate {
    archivesBaseName = "ToolBelt-${project.MC_VERSION}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

minecraft {
    //mappings channel: 'official', version: '1.18.2'
    mappings channel: 'parchment', version: '2022.03.13-1.18.2'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            property 'legacyClassPath', '{minecraft_classpath}'

            ideaModule "${project.name}.main"

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }

        client2 {
            parent runs.client
            args '--username', 'Dev2'
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            ideaModule "${project.name}.main"

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'toolbelt', '--all', '--output', file('src/main/generated_resources/'), '--existing', file('src/main/resources/')

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    runtimeMod { transitive = false }
    runtimeOnly.extendsFrom(runtimeMod)
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.0.44'

    compileOnly fg.deobf("dev.gigaherz.sewingkit:SewingKit-1.18.2:1.5.9")
    runtimeOnly fg.deobf("dev.gigaherz.sewingkit:SewingKit-1.18.2:1.5.9")

    compileOnly fg.deobf("mezz.jei:jei-1.18.2:9.7.0.180:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.18.2:9.7.0.180")

    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.18.2-5.0.6.3")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.18.2-5.0.6.3")
}

project.afterEvaluate {
    task copyMods(type:Copy) {
        from configurations.runtimeMod.files
        into file("run/mods")
    }
    tasks.forEach {
        if (it.name.startsWith("prepareRun"))
            it.dependsOn copyMods
    }
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
                "Specification-Title": "toolbelt",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
